import std.util as *

fn path_join(string dst, string src):string {
    if src.len() == 0 {
        return dst
    }

    if dst.len() == 0 {
        return src
    }

    // src 是绝对路径时不进行拼接
    if src[0] == ascii('/') {
        return src
    }

    var dst_list = dst as [u8]
    var len = dst_list.len()

    // 去掉结尾的 /
    if dst_list[len - 1] == ascii('/') {
        dst_list = dst_list.slice(0, len - 1)
    }

    var result = ''

    // ./
    if src[0] == ascii('.') && src[1] == ascii('/') {
        for int i = 1; i < src.len(); i+=1 {
            dst_list.push(src[i])
        }

        result = dst_list as string
    } else {
        // ../ not handle, direct join
        result = dst_list as string + '/' + src
    }

    return result
}

// 读取命令行参数
fn args():[string] {
    return std_args()
}

// 字符串格式化
fn sprintf(string fmt, ...[any] args):string {
    var end = fmt.len()
    var result = [] as [u8]

    var sources = fmt as [u8]

    int i = 0
    int arg_index = 0

    for i < end {
        u8 c = sources[i]
        if c != ascii('%') {
            result.push(c)
            i += 1
            continue
        }

        i += 1 // 跳过 % 字符串
        
        // 最后一个字符是一个 %, 但是没有后续字符了
        if i == end {
            continue
        }

        c = sources[i]
        i+=1 // 跳过 c 字符

        if c == ascii('%') {
            result.push(ascii('%'))
            continue
        }

        // c 不再 format 字符中，提示异常
        if !{ascii('v'), ascii('c')}.has(c) || arg_index == args.len() {
            result.push(ascii('%'))
            result.push(ascii('!'))
            result.push(c)
            continue
        }

        any arg = args[arg_index]
        arg_index += 1

        if c == ascii('c') {
            result.push(arg as u8)
            continue
        }

        // 各种类型的值进行 string 处理
        if c == ascii('v') {
            string s = ''
            if arg is int {
                 s = itos(arg as int)
            }  else if arg is i8 {
                 s = itos(arg as i8)
            }  else if arg is i16 {
                 s = itos(arg as i16)
            } else if arg is i32 {
                 s = itos(arg as i32)
            } else if arg is i64 {
                 s = itos(arg as i64)
            } else if arg is uint {
                 s = itos(arg as uint)
            } else if arg is u8 {
                 s = itos(arg as u8)
            }  else if arg is u16 {
                 s = itos(arg as u16)
            } else if arg is u32 {
                 s = itos(arg as u32)
            } else if arg is u64 {
                 s = itos(arg as u64)
            } else if arg is float {
                 s = ftos(arg as float)
            } else if arg is f64 {
                 s = ftos(arg as f64)
            } else if arg is f32 {
                 s = ftos(arg as f32)
            } else if arg is bool {
                let arg as bool
                if arg {
                    s = 'true'
                } else {
                    s = 'false'
                }
            } else if arg is string {
                s = arg as string
            } else {
                s = '%!v'
            }

            for c in s as [u8] { // c 是一个新的值，不会和上面的 c 冲突
                result.push(c)
            }

            continue
        }
    }

    return result as string
}

fn printf(string fmt, ...[any] args) {
    var str = sprintf(fmt, ...args)
    print(str)
}
