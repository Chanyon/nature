import fmt.utils as *

fn sprintf(string format, ...[any] args):string {
    var end = format.len()
    var result = vec_new<u8>()

    var sources = format as [u8]

    int i = 0
    int arg_index = 0

    for i < end {
        u8 c = sources[i]
        i += 1
        if c != ascii('%') {
            result.push(c)
            continue
        }

        if i == end {
            continue
        }

        // 宽度字符解析, 如 %018， %18 这两种模式的宽度和填充字符解析
        var fill = ' '
        var width = 0 as u8

        if sources[i] == ascii('0') {
            fill = '0'
            i+=1
        } 

        for true {
            if sources[i] < ascii('0') || sources[i] > ascii('9') {
                break
            }

            width = width * 10 + (sources[i] - ascii('0'))
            i += 1
        }

        c = sources[i]
        i+=1

        if c == ascii('%') {
            result.push(ascii('%'))
            continue
        }

        if arg_index == args.len() {
             result.push(ascii('%'))
             result.push(ascii('!'))
             result.push(c)
             continue
        }

        // c 不再 format 字符中，提示异常
        if !{ascii('v'), ascii('c'), ascii('d'), ascii('s')}.contains(c) {
            result.push(ascii('%'))
            result.push(ascii('?'))
            result.push(c)
            continue
        }

        any arg = args[arg_index]
        arg_index += 1

        if c == ascii('d') {
            string s = ''
            if arg is int {
                 s = itos_with(arg as int, fill, width)
            }  else if arg is i8 {
                 s = itos_with(arg as i8, fill, width)
            }  else if arg is i16 {
                 s = itos_with(arg as i16, fill, width)
            } else if arg is i32 {
                 s = itos_with(arg as i32, fill, width)
            } else if arg is i64 {
                 s = itos_with(arg as i64, fill, width)
            } else if arg is uint {
                 s = itos_with(arg as uint, fill, width)
            } else if arg is u8 {
                 s = itos_with(arg as u8, fill, width)
            }  else if arg is u16 {
                 s = itos_with(arg as u16, fill, width)
            } else if arg is u32 {
                 s = itos_with(arg as u32, fill, width)
            } else if arg is u64 {
                 s = itos_with(arg as u64, fill, width)
            }  else {
                 s = '%!d'
            }

            for c in s {
                result.push(c)
            }

            continue
        }

        if c == ascii('s') {
            string s = ''
            if arg is string {
                s = arg // auto as
            } else {
                s = '%!s'
            }
            for c in s {
                result.push(c)
            }

            continue
        }

        if c == ascii('c') {
            if !(arg is u8) {
                result.push(ascii('%'))
                result.push(ascii('!'))
                result.push(ascii('c'))
                continue
            }

            result.push(arg as u8)
            continue
        }

        // 各种类型的值进行 string 处理
        if c == ascii('v') {
            string s = ''
            if arg is int {
                 s = itos(arg)
            }  else if arg is i8 {
                 s = itos(arg)
            }  else if arg is i16 {
                 s = itos(arg)
            } else if arg is i32 {
                 s = itos(arg)
            } else if arg is i64 {
                 s = itos(arg)
            } else if arg is uint {
                 s = itos(arg)
            } else if arg is u8 {
                 s = itos(arg)
            }  else if arg is u16 {
                 s = itos(arg)
            } else if arg is u32 {
                 s = itos(arg)
            } else if arg is u64 {
                 s = itos(arg)
            } else if arg is float {
                 s = ftos(arg)
            } else if arg is f64 {
                 s = ftos(arg)
            } else if arg is f32 {
                 s = ftos(arg)
            } else if arg is bool {
                if arg {
                    s = 'true'
                } else {
                    s = 'false'
                }
            } else if arg is string {
                s = arg // auto as
            } else {
                s = '%!v'
            }

            for c in s {
                result.push(c)
            }

            continue
        }
    }

    return result as string
}

fn printf(string format, ...[any] args) {
    var str = sprintf(format, ...args)
    print(str)
}