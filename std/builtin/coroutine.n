import coroutine.utils
import runtime

type future_t<T> = struct{
    i64 size
    raw_ptr<T> result
    raw_ptr<error_t> error
    void_ptr co
}

fn async<T>(fn():void! function, int flag):ptr<future_t<T>> {
    var fu = new future_t<T>
    fu.size = @sizeof(T)
    if fu.size > 0 {
        var hash = @reflect_hash(T)
        fu.result = runtime.gc_malloc(hash) as raw_ptr<T>
    }

    // Establish mutually binding relationships, so even if the coroutine exits,
    // the related result/error will also be bound to the future to prevent being garbage collected.
    fu.co = utils.coroutine_async(function as void_ptr, flag, fu as void_ptr)

    return fu
}

fn co_return<T>(raw_ptr<T> result) {
    utils.coroutine_return(result as void_ptr)
}

fn future_t<T>.await():T! {
    utils.coroutine_await(self.co)

    if self.error != null {
        throw self.error.msg
    }

    return *self.result
}

fn future_t<T:void>.await():void! {
    utils.coroutine_await(self.co)

    if self.error != null {
        throw self.error.msg
    }

    return
}